
Функция УбратьЛишниеПробелыИТабы(Знач Строка) Экспорт
	//Строка = СтрЗаменить(Строка, Символы.Таб, "");
	Строка = СокрЛП(Строка);
	
	КоличествоПробелов = 0;
	Пробел = " ";
	НеразрывныйПробел = Символы.НПП;
	НоваяСтрока = "";
	
	Пока Не ПустаяСтрока(Строка) Цикл
		ТекущийСимвол = Лев(Строка, 1);
		Если Ложь
			Или ТекущийСимвол = Пробел
			Или ТекущийСимвол = НеразрывныйПробел
			Тогда
			
			КоличествоПробелов = КоличествоПробелов + 1;
			
		Иначе
			КоличествоПробелов = 0;	
			
		КонецЕсли;
		
		Если Не КоличествоПробелов > 1 Тогда
			НоваяСтрока = НоваяСтрока + ТекущийСимвол;
		КонецЕсли;
		
		Строка = Сред(Строка, 2);
	КонецЦикла;
	
	Возврат СтрЗаменить(НоваяСтрока, НеразрывныйПробел, Пробел);
КонецФункции

Функция ИмяКаталогаПроекта() Экспорт
	Возврат "features";
КонецФункции

Функция ПолучитьРасширениеФичи() Экспорт
	Возврат ".feature";
КонецФункции

Функция ПолучитьИмяПеременнойИзСтроки(Знач ИсходнаяСтрока) Экспорт
	ИсходнаяСтрока = СокрЛП(ИсходнаяСтрока);
	ИтоговаяСтрока = "";
	
	Пробел = " ";
	НижнееПодчеркивание = "_";
	
	КрайнийСимвол = Истина;
	
	ПроверочнаяСтрока = ПолучитьПроверочнуюСтроку();
	
	ДлинаСтроки = СтрДлина(ИсходнаяСтрока);
	Для Счетчик = 1 По ДлинаСтроки Цикл
		ТекущийСимвол = Сред(ИсходнаяСтрока, Счетчик, 1);
		
		Если Истина
			И ТекущийСимвол = Пробел
			И Не КрайнийСимвол = НижнееПодчеркивание
			Тогда
			
			ИтоговаяСтрока = ИтоговаяСтрока + "_";
			
		Иначе
			Если Найти(ПроверочнаяСтрока, ТекущийСимвол) > 0 Тогда
				ИтоговаяСтрока = ИтоговаяСтрока + ТекущийСимвол;
				
			КонецЕсли;
		КонецЕсли;
		
		КрайнийСимвол = Прав(ИтоговаяСтрока, 1);
	КонецЦикла;
	
	ПервыйСимвол = Лев(ИтоговаяСтрока, 1);
	Если Найти("0123456789", ПервыйСимвол) > 0 Тогда
		ИтоговаяСтрока = НижнееПодчеркивание + ИтоговаяСтрока;
	КонецЕсли;
	
	Возврат ИтоговаяСтрока;
КонецФункции

Функция ПолучитьПроверочнуюСтроку()
	КириллицаЗаглавные = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ";
	КириллицаСтрочные = НРег(КириллицаЗаглавные);
	
	ЛатиницаЗаглавные = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
	ЛатиницаСтрочные = НРег(ЛатиницаЗаглавные);
	
	Цифры = "0123456789";
	
	ПроверочнаяСтрока = 
	КириллицаЗаглавные +
	КириллицаСтрочные +
	ЛатиницаЗаглавные +
	ЛатиницаСтрочные +
	Цифры;
	
	Возврат ПроверочнаяСтрока;
КонецФункции